(ruleset optim)

(rewrite (Add x (Const 0.)) x :ruleset optim)
(rewrite (Add (Const 0.) x) x :ruleset optim)
(rewrite (Mul x (Const 1.)) x :ruleset optim)
(rewrite (Mul (Const 1.) x) x :ruleset optim)
(rewrite (Mul x (Const 0.)) (Const 0.) :ruleset optim)
(rewrite (Mul (Const 0.) x) (Const 0.) :ruleset optim)
(rewrite (Add (Mul x y) (Mul x z)) (Mul x (Add y z)) :ruleset optim)
(rewrite (Add (Mul x z) (Mul y z)) (Mul (Add x y) z) :ruleset optim)

(rewrite (Get (Build n f) i) (App f i) :subsume :ruleset optim)
(rewrite (Length (Build n f)) n :ruleset optim)

(rewrite (Cond p e e) e :ruleset optim)
(rewrite (App g (Cond p t f)) (Cond p (App g t) (App g f)) :ruleset optim)

(rewrite (IFold f z (Int 0)) z :ruleset optim)
(rewrite (IFold f z (Int n)) (IFold (Lam (Lam (App (App (Shift 0 2 f) (Var 1)) (Add (Var 0) (Int 1))))) (App (App f z) (Int 0)) (Int (- n 1))) :when ((> n 0)) :ruleset optim)
(rewrite (IFold (Lam (Lam (Var 1))) z n) z :ruleset optim)

(rewrite (Fst (Pair x y)) x :subsume :ruleset optim)
(rewrite (Snd (Pair x y)) y :subsume :ruleset optim)

(rewrite (IFold (Lam (Lam (Pair (App (App f0 (Fst (Var 1))) (Var 0)) (App (App f1 (Snd (Var 1))) (Var 0))))) (Pair z0 z1) n) (Pair (IFold f0 z0 n) (IFold f1 z1 n)) :ruleset optim)

(constructor Subst (i64 Expr Expr) Expr :unextractable)

(rewrite (App (Lam f) x) (Shift 0 -1 (Subst 0 (Shift 0 1 x) f)) :subsume :ruleset optim)

(rewrite (Subst n e (App f x)) (App (Subst n e f) (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Lam f)) (Lam (Subst (+ n 1) (Shift 0 1 e) f)) :ruleset optim)
(rewrite (Subst n e (Var n)) e :ruleset optim)
(rewrite (Subst n e (Var m)) (Var m) :when ((!= n m)) :ruleset optim)
(rewrite (Subst n e (Int i)) (Int i) :ruleset optim)
(rewrite (Subst n e (Const x)) (Const x) :ruleset optim)
(rewrite (Subst n e (Cond x y z)) (Cond (Subst n e x) (Subst n e y) (Subst n e z)) :ruleset optim)
(rewrite (Subst n e (Add x y)) (Add (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Sub x y)) (Sub (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Mul x y)) (Mul (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Div x y)) (Div (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Pow x y)) (Pow (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Neg x)) (Neg (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Exp x)) (Exp (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Log x)) (Log (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Sin x)) (Sin (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Cos x)) (Cos (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (LT x y)) (LT (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (GT x y)) (GT (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (EQ x y)) (EQ (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (And x y)) (And (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Or x y)) (Or (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Not x)) (Not (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Build x y)) (Build (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (IFold x y z)) (IFold (Subst n e x) (Subst n e y) (Subst n e z)) :ruleset optim)
(rewrite (Subst n e (Get x y)) (Get (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Length x)) (Length (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Pair x y)) (Pair (Subst n e x) (Subst n e y)) :ruleset optim)
(rewrite (Subst n e (Fst x)) (Fst (Subst n e x)) :ruleset optim)
(rewrite (Subst n e (Snd x)) (Snd (Subst n e x)) :ruleset optim)
